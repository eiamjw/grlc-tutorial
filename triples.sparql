#+ summary: Largest cities per country
#+ endpoint: https://query.wikidata.org/sparql
#This is the syntax for GRLC metadata
PREFIX wdt:        <http://www.wikidata.org/prop/direct/>

SELECT DISTINCT ?city ?cityLabel ?population ?country ?countryLabel ?loc WHERE {
	{
		SELECT (MAX(?population) AS ?population) ?country WHERE {
			?city wdt:P31/wdt:P279* wd:Q515 .
			?city wdt:P1082 ?population .
			?city wdt:P17 ?country .
		}
		GROUP BY ?country
		ORDER BY DESC(?population)
	}
	?city wdt:P31/wdt:P279* wd:Q515 .
	?city wdt:P1082 ?population .
	?city wdt:P17 ?country .
	?city wdt:P625 ?loc .
	SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en" .
	}
}
